plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

apply from: '../buildSystem/enviroments.gradle'
apply from: '../buildSystem/keystore.gradle'

def versionBuild = 1

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.reqres.poc"

        def versionTag = getVersionByGitTag()
        versionCode generateVersionCode(versionTag, versionBuild)
        versionName generateVersionName(versionTag)
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['releaseAlias']
            keyPassword keystoreProperties['releasePassword']
        }
        debug {
            initWith release
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            versionNameSuffix ".${versionBuild}-debug"
        }
    }

    buildTypes.each {
        / resvalues /
        it.resValue "string", "app_name", appName[it.name]

        / build config fields /
        it.buildConfigField "String", "REQRES_API_URL", reqresApiUrl
    }

    buildFeatures {
        viewBinding true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def appName = rootProject.name.toLowerCase()
            outputFileName = outputFileName.replace("app-", "$appName-").replaceAll(".apk", "-v${variant.versionName.replaceAll("-debug|-release", "")}.apk")
            log("yellow", outputFileName)
        }
    }
}

dependencies {
    / Modules /
    implementation project(':ui')
}